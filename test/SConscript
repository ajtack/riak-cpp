Import('env')
Import('library')

unit_tests_env = env.Clone()
unit_tests_env.Append(
	CPPPATH = [
		'#ext/gmock-1.7.0/include',
		'#ext/gmock-1.7.0/gtest/include'
		],
	)

compilation_units = [
		Glob('units/*.cxx'),
		Glob('fixtures/*.cxx'),
		Glob('fixtures/*/*.cxx'),
		Glob('mocks/*.cxx'),
		Glob('mocks/*/*.cxx'),
		Glob('mocks/*/*/*.cxx')
	]

gmock = SConscript('#ext/gmock.SConscript', 'env', variant_dir='ext/')
gtest = SConscript('#ext/gtest.SConscript', 'env', variant_dir='ext/')

necessary_libraries = env['LIBS']
necessary_libraries.extend([library, gmock, gtest])

unit_tests = unit_tests_env.Program('unit_tests', compilation_units, LIBS=necessary_libraries)
unit_tests_env.Alias('test', [unit_tests], unit_tests[0].path)
unit_tests_env.AlwaysBuild('test')
Return('unit_tests')
